plugins {
    id 'idea'
    id 'java-library'
    id 'maven-publish'
    id 'fabric-loom' version '1.11-SNAPSHOT'
}

tasks.named('wrapper', Wrapper).configure {
    // Define wrapper values here so as to not have to always do so when updating gradlew.properties.
    // Switching this to Wrapper.DistributionType.ALL will download the full gradle sources that comes with
    // documentation attached on cursor hover of gradle classes and methods. However, this comes with increased
    // file size for Gradle. If you do switch this to ALL, run the Gradle wrapper task twice afterwards.
    // (Verify by checking gradle/wrapper/gradle-wrapper.properties to see if distributionUrl now points to `-all`)
    distributionType = Wrapper.DistributionType.BIN
}

version = project.mod_version
group = project.mod_group_id

base {
    archivesName = project.mod_id
}
loom {
    accessWidenerPath = file("src/main/resources/acceleratedrendering.accesswidener")
}

repositories {
    mavenLocal()
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }

    maven {
        url 'https://jitpack.io'
        content {
            includeGroup "com.github.TartaricAcid"
        }
    }

    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
    maven {
        name = "Terraformers"
        url = "https://maven.terraformersmc.com/"
    }
    maven {
        name = "Fuzs Mod Resources"
        url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
    }
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
    }
    maven {
        name = "Ladysnake Libs"
        url = 'https://maven.ladysnake.org/releases'
    }
}

base {
    archivesName = mod_id
}

// Mojang ships Java 17 to end users in 1.20.1, so mods should target Java 17.
java {
    sourceCompatibility = 17
    targetCompatibility = 17
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}


dependencies {
    // To change the versions see the gradle.properties file
    minecraft "net.minecraft:minecraft:${project.minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${project.parchment_minecraft_version}:${project.parchment_mappings_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    //modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation fabricApi.module("fabric-resource-loader-v0", project.fabric_version)
    modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}")
    modImplementation("fuzs.forgeconfigapiport:forgeconfigapiport-fabric:8.0.2") {
        exclude group: "net.fabricmc"
        exclude group: "net.fabricmc.fabric-api"
    }
    modCompileOnly "maven.modrinth:sodium:mc1.20.1-0.5.13-fabric"
    modCompileOnly "maven.modrinth:iris:1.7.6+1.20.1"
    modCompileOnly "maven.modrinth:geckolib:AXhbVyuq"
    modCompileOnly "maven.modrinth:entity-model-features:ZQTkVFpd"
    modCompileOnly "maven.modrinth:immediatelyfast:1.5.2+1.20.4-fabric"
    modCompileOnly "maven.modrinth:axiom:XVGX75oo"
    compileOnly("com.moulberry:mixinconstraints:1.0.9")
    compileOnly("org.projectlombok:lombok:1.18.38")
    annotationProcessor("org.projectlombok:lombok:1.18.38")
    modCompileOnly "dev.emi:trinkets:3.7.2"
    modCompileOnly "maven.modrinth:accessories:1.0.0-beta.47+1.20.1"
    modCompileOnly "maven.modrinth:malilib:V7yLDtJV" // 0.16.3
    modCompileOnly "maven.modrinth:tweakermore:2auQS5xh" // v3.27.0-mc1.20.1
    modCompileOnly "maven.modrinth:touhoulittlemaid-orihime:a6vnKr9N"
//    //implementation "com.github.tartaricacid:touhoulittlemaid-1.21.1:release-1.2.2"
}
processResources {
    var replaceProperties = [
            minecraft_version: minecraft_version,
            mod_id           : mod_id,
            mod_name         : mod_name,
            mod_license      : mod_license,
            mod_version      : mod_version,
            mod_authors      : mod_authors,
            mod_description  : mod_description
    ]
    inputs.properties replaceProperties

    filesMatching("fabric.mod.json") {
        expand replaceProperties
    }
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = false
        downloadJavadoc = false
    }
}
